plugins {
    id 'java-gradle-plugin'
    id 'org.unbroken-dome.test-sets' version '1.5.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id "com.bmuschko.nexus" version "2.3.1"
}
apply plugin: 'groovy'

group 'fi.linuxbox.gradle'
version '0.4-SNAPSHOT'
ext {
    pluginId = "fi.linuxbox.download.worker"
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

modifyPom {
    project {
        name 'gradle-download-worker'
        description 'Parallel Download Worker Gradle plugin'
        url 'https://github.com/vmj/gradle-download-worker'

        licenses {
            license {
                name 'GNU General Public License, version 3'
                url 'http://www.gnu.org/licenses/gpl-3.0.html'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'vmj'
                name 'Mikko VÃ¤rri'
                email 'mikko@varri.fi'
            }
        }

        scm {
            url 'https://github.com/vmj/gradle-download-worker'
            connection 'scm:https://github.com/vmj/gradle-download-worker.git'
            developerConnection 'scm:git@github.com:vmj/gradle-download-worker.git'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

testSets {
    functionalTest
}
check.dependsOn functionalTest

functionalTest {
    if(gradle.startParameter.isOffline()) {
        systemProperties.IS_OFFLINE = '1'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'

    functionalTestCompile("org.spockframework:spock-core:1.1-groovy-${spockGroovyVer}") {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest

    plugins {
        "DownloadWorkerPlugin" {
            id = pluginId
            implementationClass = "fi.linuxbox.gradle.download.worker.DownloadWorkerPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/vmj/gradle-download-worker'
    vcsUrl = 'https://github.com/vmj/gradle-download-worker'

    plugins {
        "DownloadWorkerPlugin" {
            id = pluginId
            displayName = "Parallel Download Worker Gradle plugin"
            description = "Parallel Download Worker Gradle plugin. Only for Java >=8 and Gradle >=4.0"
            tags = ['download', 'worker']
        }
    }
}
