plugins {
    id 'com.gradle.build-scan' version '1.10.1'
    id 'java-gradle-plugin'
    id 'org.unbroken-dome.test-sets' version '1.4.2'
    id 'com.gradle.plugin-publish' version '0.9.7'
}
apply plugin: 'groovy'

group 'fi.linuxbox'
version '0.3'
ext {
    pluginId = "fi.linuxbox.download.worker"
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

testSets {
    integrationTest
    functionalTest
}
check.dependsOn integrationTest
check.dependsOn functionalTest

functionalTest {
    if(gradle.startParameter.isOffline()) {
        systemProperties.IS_OFFLINE = '1'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'

    functionalTestCompile("org.spockframework:spock-core:1.1-groovy-${spockGroovyVer}") {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest

    plugins {
        "DownloadWorkerPlugin" {
            id = pluginId
            implementationClass = "fi.linuxbox.gradle.download.worker.DownloadWorkerPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/vmj/gradle-download-worker'
    vcsUrl = 'https://github.com/vmj/gradle-download-worker'

    plugins {
        "DownloadWorkerPlugin" {
            id = pluginId
            displayName = "Parallel Download Worker Gradle plugin"
            description = "Parallel Download Worker Gradle plugin. Only for Java >=8 and Gradle >=4.0"
            tags = ['download', 'worker']
        }
    }
}
